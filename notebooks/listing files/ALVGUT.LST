ALVGUT	ATARI MAC65 VM03.09  00:01:07
TABLE OF CONTENTS

   1-  2 VGUT-VECTOR GENERATOR UTILITIES
   1-  5 **************************************************
   1-  6 *						*
   1-  7 *MODULE:		ALCOMN				*
   1-  8 *PROGRAMMER:	DAVE THEURER			*
   1-  9 *FUNCTION:	ALIENS CONSTANTS AND VARIABLES	*
   1- 10 *		TO BE .INCLUDED IN:		*
   1- 11 *		    ALGAME,ALDISP,ALHARD	*
   1- 12 *						*
   1- 13	COPYRIGHT 1980 ATARI, INC.  UNAUTHORIZED REPRODUCTION,
   1- 14	ADAPTION, DISTRIBUTION, PERFORMANCE OR DISPLAY OF THIS
   1- 15	COMPUTER PROGRAM OR THE ASSOCIATED AUDIOVISUAL WORK IS
   1- 16	STRICTLY PROHIBITED.
   1- 17 **************************************************
   2-  4 CONSTANTS-COUNTS
   2- 25 CONSTANTS-STATE CODES
   4-  1 CONSTANTS-PICTURES
   4- 22	BOOM
   6-  1	HARDWARE DEFINITIONS
   7-  1 VARIABLES-CONTROL
   8-  1 VARIABLES-WORK
   8- 22 VARIABLES-PLAYERS
   8- 41 VARIABLES-SWITCHES
   9-  1 VARIABLES-DISPLAY
  11- 25	VARIABLES - PLAY
  12-  1	CONSTANTS-COUNTS
  12- 12	CONSTANTS-PLAYFIELD
  12- 17	VARIABLES-OBJECT COUNTERS
  13-  1	VARIABLES-OBJECT LOCATION + STATUS
  13-137	VARIABLES - PAGE 1
  15- 18*********************************************
  15- 19*
  15- 20*PROGRAMMER:  LOGG/THEURER
  15- 21*
  15- 22*CHECKER:
  15- 23*
  15- 24*********************************************
  15- 25 VGUT - VECTOR GENERATOR UTILITY SUBROUTINES
  17-  4 VGRTSL - ADD RTSL TO VECTOR LIST
  17- 15 VGHALT - CENTER AND HALT VECTOR GENERATOR
  17- 29 VGHEXZ - DISPLAY DIGIT WITH ZERO SUPPRESSION
  17- 45 VGHEX - DISPLAY DIGIT
  17- 72 VGJSRL - ADD JSRL TO VECTOR LIST
  17- 94 VGSTAT - SET VECTOR GENERATOR STATUS
  17-110 VGCNTR - CENTER BEAM IN MIDDLE OF SCREEN
  17-122 VGADD2 - ADD 2 WORDS TO VECTOR LIST
  17-138 VGADD - ADD Y+1 TO VECTOR ADDRESS
  17-155 VGSCAL-SET VECTOR GENERATOR SCALE
  17-170 VGVTR - SHORT FORM VGVCTR CALL
  17-203 VGVCTR - ADD VECTOR TO VECTOR LIST
  17-235	DISPLAY DIGITS
ALVGUT	ATARI MAC65 VM03.09  00:01:07 PAGE 1


     1                                  .TITLE  ALVGUT
     2                                          .SBTTL VGUT-VECTOR GENERATOR UTILITIES
ALVGUT	ATARI MAC65 VM03.09  00:01:07 PAGE 15
VARIABLES - PAGE 1


     3                                  ;ZERO PAGE GLOBALS
     4
     5                                          .GLOBL XCOMP,VGLIST,VGBRIT
     6
     7                                  ;EXTERNAL ENTRY POINTS
     8
     9                                          .GLOBL  VGMSGA
    10
    11                                  ;ENTRY POINTS
    12
    13                                          .GLOBL VGADD,VGHALT,VGHEX,VGHEXZ,VGVTR
    14                                          .GLOBL VGJSRL,VGADD2,VGVTR1,VGVTR2,VGSTA1,VGADD3
    15                                          .GLOBL VGRTSL,VGVCTR,VGCNTR,VGSTAT,VGSCAL,VGSCA1,DIGTYS,DIGITS
    16
    17
    18                                          .SBTTL*********************************************
    19                                          .SBTTL*
    20                                          .SBTTL*PROGRAMMER:  LOGG/THEURER
    21                                          .SBTTL*
    22                                          .SBTTL*CHECKER:
    23                                          .SBTTL*
    24                                          .SBTTL*********************************************
    25                                          .SBTTL VGUT - VECTOR GENERATOR UTILITY SUBROUTINES
    26                                  ;
    27                                  ;DATE INITIATED:	10-OCT-79
    28                                  ;PROJECT CHARGE #:	23803
    29                                  ;
    30                                  ;DISK #:	 	105, B46
    31                                  ;
    32                                  ;HARWARE REQUIREMENTS:	ANALOG AUTO-NORMALIZING VECTOR GENERATOR
    33                                  ;
    34                                  ;MEMORY REQUIREMENTS:
    35                                  ;	NOT APPLICABLE - SUBROUTINE
    36                                  ;
    37                                  ;INTERRUPT REQUIREMENTS:
    38                                  ;	NOT APPLICABLE - SUBROUTINE
    39                                  ;
    40                                  ;ASSEMBLY COMMAND STRING:
    41                                  ;	R MAC65
    42                                  ;	DX1:VGUT,DK1:VGUT=DX1:VGUT/C
    43                                  ;
    44                                  ;LINK COMMAND STRING:
    45                                  ;	NOT APPLICABLE - SUBROUTINE
    46                                  ;
    47                                  ;PROGRAM DESCRIPTION:
    48                                  ;	A SET OF UTILITY ROUTINES FOR GENERATING
    49                                  ;	VECTORS USING THE ANALOG AUTO-NORMALIZING VECTOR GENERATOR.
ALVGUT	ATARI MAC65 VM03.09  00:01:07 PAGE 16
VGUT - VECTOR GENERATOR UTILITY SUBROUTINES

     1                                  ;ZERO PAGE GLOBALS REQUIRED:
     2                                  ;
     3                                  ;	VGLIST:	THIS 2 BYTE VARIABLE CONTAINS THE CURRENT VECTOR GENERATOR RAM
     4                                  ;		ADDRESS USED TO BUILD INSTRUCTIONS FOR THE VECTOR GENERATOR.
     5                                  ;		IT SHOULD BE INITIALIZED BEFORE CALLING ANY OF THESE ROUTINES.
     6                                  ;
     7                                  ;	XCOMP:	THIS 4 BYTE VARIABLE IS USED TO CONTAIN THE X (LSB,MSB) COMPONENT
     8                                  ;		AND Y (LSB,MSB) COMPONENT USED IN SEVERAL OF THE VECTOR INSTRUC-
     9                                  ;		TIONS.  FOR THE VGVCTR ROUTINE THESE FIELDS ARE SIGNED 2'S
    10                                  ;		COMPLEMENT NUMBERS.
    11                                  ;
    12                                  ;	VGBRIT: THIS VARIABLE IS USED BY VGVCTR AND VGSTAT TO GENERATE VECTORS
    13                                  ;		WITH THE GIVEN BRIGHTNESS. IT'S VALUES ARE 0,10,20,30,
    14                                  ;		40,...F0 WHERE 0 IS OFF AND F0 IS MAX BRIGHTNESS.
    15                                  ;		IN THE VECTOR INSTRUCTIONS ONLY THE UPPER 3 BITS IS USED
    16                                  ;		IF Z=1 IN THE VECTOR INSTRUCTIONS THEN THE LAST NON-ZERO
    17                                  ;		Z IS USED.
    18                                  ;
    19                                  ;
    20                                  ;EXTERNAL ENTRY POINTS REQUIRED:
    21                                  ;
    22                                  ;	VGMSGA:	THIS ENTRY POINT PROVIDES JSRL INSTRUCTIONS TO THE CHAR.X
    23                                  ;		ROUTINES.  THIS ENTRY POINT IS PROVIDED BY VECAN.MAC.
    24                                  ;
    25                                  ;THESE ROUTINES WERE WRITTEN TO PROVIDE PROGRAMMERS USING LYLE RAIN'S VECTOR
    26                                  ;GENERATOR A MEANS OF:
    27                                  ;	1)	DYNAMICALLY GENERATING VECTORS JUST AS VECMAC ALLOWS STATIC
    28                                  ;		GENERATION OF VECTORS.
    29                                  ;	2)	DISPLAY NUMBERS WITH OR WITHOUT ZERO SUPPRESSION.
    30                                  ;
    31                                  ;EXAMPLE 1:
    32                                  ;
    33                                  ;	LDA I,VECRAM&0FF	;INITIALIZE VECTOR RAM POINTER
    34                                  ;	STA VGLIST
    35                                  ;	LDA I,VECRAM/100
    36                                  ;	STA VGLIST+1
    37                                  ;	JSR CENTER		;CENTER BEAM IN MIDDLE OF SCREEN.
    38                                  ;	LDA I,256./4		;POSITION BEAM AT (256,256)
    39                                  ;	LDX I,256./4
    40                                  ;	JSR VGSVTR
    41                                  ;	ETC....
    42                                  ;
    43                                  ;
    44                                  ;EXAMPLE 2:
    45                                  ;
    46                                  ;	LDA PLAYER		;DISPLAY PLAYER NUMBER
    47                                  ;	JSR VGHEX		;REMEMBER BEAM MUST BE POSITIONED CORRECTLY
    48                                  ;	LDA SCORE1		;MSB OF SCORE
    49                                  ;	LSR
    50                                  ;	LSR
    51                                  ;	LSR
    52                                  ;	LSR
    53                                  ;	SEC
    54                                  ;	JSR VGHEXZ		;DISPLAY UPPER DIGIT WITH ZERO SUPPRESSION
    55                                  ;	LDA SCORE1
    56                                  ;	JSR VGHEXZ		;DISPLAY SECOND DIGIT WITH ZERO SUPPRESSION
    57                                  ;	LDA SCORE0		;LSB OF SCORE
ALVGUT	ATARI MAC65 VM03.09  00:01:07 PAGE 16+
VGUT - VECTOR GENERATOR UTILITY SUBROUTINES

    58                                  ;	PHP			;SAVE ZERO SUPPRESSION FLAG
    59                                  ;	LSR
    60                                  ;	LSR
    61                                  ;	LSR
    62                                  ;	LSR
    63                                  ;	PLP
    64                                  ;	JSR VGHEXZ		;DISPLAY THIRD DIGIT WITH ZERO SUPPRESSION
    65                                  ;	LDA SCORE0
    66                                  ;	JSR VGHEX		;DISPLAY LAST DIGIT
    67                                  ;	ETC....
ALVGUT	ATARI MAC65 VM03.09  00:01:07 PAGE 17
VGUT - VECTOR GENERATOR UTILITY SUBROUTINES

     1
     2
     3         0000'                            .CSECT
     4                                          .SBTTL VGRTSL - ADD RTSL TO VECTOR LIST
     5                                  ;VGRTSL - ADD RTSL TO VECTOR LIST
     6                                  ;
     7                                  ;ENTRY	(VGLIST,VGLIST+1) = VECTOR LIST ADDRESS
     8                                  ;EXIT	(VGLIST,VGLIST+1) = NEW VECTOR LIST ADDRESS
     9                                  ;USES	A,Y,(VGLIST,VGLIST+1)
    10
    11   0000    A9    C0               VGRTSL: LDA I,0C0               ;DXXX IS RTSL
    12   0002    D0    05                       BNE VGHAL1              ;ALWASY
    13
    14
    15                                          .SBTTL VGHALT - CENTER AND HALT VECTOR GENERATOR
    16                                  ;VHALT - CENTER AND HALT VECTOR GENERATOR
    17                                  ;
    18                                  ;ENTRY	(VGLIST,VGLIST+1) = VECTOR LIST ADDRESS
    19                                  ;EXIT	(VGLIST,VGLIST+1) = NEW VECTOR LIST ADDRESS
    20                                  ;USES	A,X,Y,(VGLIST,VGLIST+1)
    21
    22   0004    20  004A'              VGHALT: JSR VGCNTR              ;CENTER FIRST
    23   0007    A9    20                       LDA I,20                ;BXXX IS HALT
    24   0009    A0    00               VGHAL1: LDY I,0
    25   000B    91    74                       STA NY,VGLIST
    26   000D    4C  00A3'                      JMP VGWAI1              ;ADD LAST BYTE
    27
    28
    29                                          .SBTTL VGHEXZ - DISPLAY DIGIT WITH ZERO SUPPRESSION
    30                                  ;VGHEXZ - DISPLAY DIGIT WITH ZERO SUPPRESSION
    31                                  ;
    32                                  ;THIS ROUTINE WILL DISPLAY A DIGIT USING THE DEFAULT CHARACTER SIZE.
    33                                  ;NO ATTEMPT IS MADE TO USE THE VARIABLE VGSIZE.
    34                                  ;
    35                                  ;ENTRY	(A) = LOWER 4 BITS TO BE DISPLAYED
    36                                  ;	(C) = CARRY CLEAR IF NO ZERO SUPPRESSION
    37                                  ;EXIT	(C) = CARRY CLEARED IF NON-ZERO DIGIT DISPLAYED
    38                                  ;USES	A,X,Y,(VGLIST,VGLIST+1)
    39
    40   0010    90    04               VGHEXZ: BCC VGHEX               ;IF NO ZERO SUPPRESSION
    41   0012    29    0F                       AND I,0F
    42   0014    F0    05                       BEQ VGHEX1              ;LEAVE C SET
    43
    44
    45                                          .SBTTL VGHEX - DISPLAY DIGIT
    46                                  ;VGHEX - DISPLAY DIGIT
    47                                  ;
    48                                  ;THIS ROUTINE WILL DISPLAY A DIGIT USING THE DEFAULT CHARACTER SIZE.
    49                                  ;NO ATTEMPT IS MADE TO USE THE VARIABLE VGSIZE.
    50                                  ;
    51                                  ;ENTRY	(VGLIST,VGLIST+1) = VECTOR LIST ADDRESS
    52                                  ;	(A) = LOWER 4 BITS TO BE DISPLAYED
    53                                  ;EXIT	(VGLIST,VGLIST+1) = NEW VECTOR LIST ADDRESS
    54                                  ;	(C) = CARRY IS CLEAR
    55                                  ;USES	A,X,Y,(VGLIST,VGLIST+1)
    56
    57   0016    29    0F               VGHEX:  AND I,0F
ALVGUT	ATARI MAC65 VM03.09  00:01:07 PAGE 17+
VGHEX - DISPLAY DIGIT

    58   0018    18                             CLC
    59   0019    69    01                       ADC I,01                ;CLEARS C BIT
    60   001B    08                     VGHEX1: PHP                     ;SAVE C FLAG
    61   001C    0A                             ASL
    62   001D    A0    00                       LDY I,0
    63   001F    AA                             TAX
    64   0020    BD  0000G                      LDA AX,VGMSGA
    65   0023    91    74                       STA NY,VGLIST
    66   0025    BD  0001G                      LDA AX,VGMSGA+1         ;COPY JSRL TO CHARACTER ROUTINE
    67   0028    C8                             INY
    68   0029    91    74                       STA NY,VGLIST
    69   002B    20  0056'                      JSR VGADD               ;UPDATE VECTOR LIST POINTER
    70   002E    28                             PLP                     ;RESTORE C FLAG
    71   002F    60                             RTS
    72                                          .SBTTL VGJSRL - ADD JSRL TO VECTOR LIST
    73                                  ;VGJSRL - ADD JSRL TO VECTOR LIST
    74                                  ;
    75                                  ;ENTRY	(VGLIST,VGLIST+1) = VECTOR LIST ADDRESS
    76                                  ;	(A) = MSB OF ADDRESS
    77                                  ;	(X) = LSB OF ADDRESS
    78                                  ;EXIT	(VGLIST,VGLIST+1) = NEW VECTOR LIST ADDRESS
    79                                  ;USES	A,Y,(VGLIST,VGLIST+1)
    80
    81   0030    4A                     VGJSRL: LSR
    82   0031    29    0F                       AND I,0F                ;BASE ADDRESS IS RELATIVE
    83   0033    09    A0                       ORA I,0A0
    84   0035    A0    01                       LDY I,01
    85   0037    91    74                       STA NY,VGLIST           ;SAVE MSB + OPCODE
    86   0039    88                             DEY
    87   003A    8A                             TXA
    88   003B    6A                             ROR
    89   003C    91    74                       STA NY,VGLIST           ;LSB OF ADDRESS
    90   003E    C8                             INY
    91   003F    D0    15                       BNE VGADD               ;UPDATE VECTOR POINTER
    92
    93
    94                                          .SBTTL VGSTAT - SET VECTOR GENERATOR STATUS
    95                                  ;VGSTAT - SET VECTOR GENERATOR STATUS
    96                                  ;
    97                                  ;ENTRY	(A)=HI/LOW AND IN/OUT FLAGS AND ENABLE (0,1,2,3,OR 4)
    98                                  ;	(Y)=VECTOR BRIGHTNESS (0,10,20,...,F0)
    99                                  ;	(VGLIST,VGLIST+1)=VECTOR LIST ADDRESS
   100                                  ;EXIT	(VGLIST,VGLIST+1)=NEW VECTOR LIST ADDRESS
   101                                  ;USES	A,X,Y,(VGLIST,VGLIST+1)
   102
   103   0041    A4    73               VGSTA1: LDY VGBRIT
   104   0043    09    60               VGSTAT: ORA I,60
   105   0045    AA                             TAX                     ;OPCODE + LIMIT BITS
   106   0046    98                             TYA
   107   0047    4C  004E'                      JMP VGADD2              ;ADD 2 BYTES TO VECTOR LIST
   108
   109
   110                                          .SBTTL VGCNTR - CENTER BEAM IN MIDDLE OF SCREEN
   111                                  ;VGCNTR - CENTER BEAM IN MIDDLE OF SCREEN
   112                                  ;
   113                                  ;ENTRY	(VGLIST,VGLIST+1)=VECTOR LIST ADDRESS
   114                                  ;USES	A,X,Y,(VGLIST,VGLIST+1)
ALVGUT	ATARI MAC65 VM03.09  00:01:07 PAGE 17+
VGCNTR - CENTER BEAM IN MIDDLE OF SCREEN

   115                                  ;
   116
   117   004A    A9    40               VGCNTR: LDA I,40                ;TIMER + SCALE
   118   004C    A2    80                       LDX I,80                ;OPCODE
   119                                  ;	JMP VGADD2
   120
   121
   122                                          .SBTTL VGADD2 - ADD 2 WORDS TO VECTOR LIST
   123                                  ;VGADD2 - ADD 2 WORDS TO VECTOR LIST
   124                                  ;
   125                                  ;ENTRY	(A)=FIRST BYTE
   126                                  ;	(X)=SECOND BYTE
   127                                  ;EXIT	(VGLIST,VGLIST+1)=NEW VECTOR LIST POINTER
   128                                  ;USES	A,Y,(VGLIST,VGLIST+1)
   129
   130   004E    A0    00               VGADD2: LDY I,0
   131   0050    91    74               VGADD3: STA NY,VGLIST           ;LSB BYTE
   132   0052    C8                             INY
   133   0053    8A                             TXA
   134   0054    91    74                       STA NY,VGLIST           ;MSB BYTE
   135                                  ;	JMP VGADD		;UPDATE VECTOR LIST POINTER
   136
   137
   138                                          .SBTTL VGADD - ADD Y+1 TO VECTOR ADDRESS
   139                                  ;VGADD - ADD Y+1 TO VECTOR LIST ADDRESS
   140                                  ;
   141                                  ;ENTRY	(VGLIST,VGLIST+1) = VECTOR LIST ADDRESS
   142                                  ;	(Y) = VALUE+1 TO BE ADDED TO VECTOR LIST
   143                                  ;EXIT	(VGLIST,VGLIST+1) = NEW VECTOR LIST ADDRESS
   144                                  ;USES	A,(VGLIST,VGLIST+1)
   145
   146   0056    98                     VGADD:  TYA                     ;ADD 1+(Y) TO VGLIST
   147   0057    38                             SEC
   148   0058    65    74                       ADC VGLIST
   149   005A    85    74                       STA VGLIST
   150   005C    90    02                       BCC 10$
   151   005E    E6    75                       INC VGLIST+1
   152   0060    60                     10$:    RTS
   153
   154
   155                                          .SBTTL VGSCAL-SET VECTOR GENERATOR SCALE
   156                                  ;VGSCAL-SET VECTOR GENERATOR SCALE
   157                                  ;
   158                                  ;ENTRY	(Y)=LINEAR SCALE FACTOR (0=FULL SIZE, FF=1/256 SIZE)
   159                                  ;	(A)=POWER OF 2 SCALE FACTOR (0=FULL SIZE,1=1/2 SIZE,...)
   160                                  ;	(VGLIST,VGLIST+1)=VECTOR LIST ADDRESS
   161                                  ;EXIT	(VGLIST,VGLIST+1)=NEW VECTOR LIST ADDRESS
   162                                  ;USES	A,X,Y(VGLIST,VGLIST+1)
   163
   164   0061    A0    00               VGSCA1: LDY I,0                 ;USE FULL SIZE
   165   0063    09    70               VGSCAL: ORA I,70                ;OPCODE + SCALE SIZE
   166   0065    AA                             TAX
   167   0066    98                             TYA
   168   0067    4C  004E'                      JMP VGADD2              ;ADD 2 BYTES TO VECTOR LIST
   169
   170                                          .SBTTL VGVTR - SHORT FORM VGVCTR CALL
   171                                  ;VGVTR - SHORT FORM VGVCTR CALL
ALVGUT	ATARI MAC65 VM03.09  00:01:07 PAGE 17+
VGVTR - SHORT FORM VGVCTR CALL

   172                                  ;
   173                                  ;ENTRY	(VGLIST,VGLIST+1) = VECTOR LIST ADDRESS
   174                                  ;	(A) = CHANGE IN X/4 (-80 TO +7F)
   175                                  ;	(X) = CHANGE IN Y/4 (-80 TO +7F)
   176                                  ;	(Y) = VECTOR BRIGHTNESS (0,10,20,...,F0)
   177                                  ;EXIT	(VGLIST,VGLIST+1) = NEW VECTOR LIST ADDRESS
   178                                  ;	(VGBRIT)=NEW VECTOR BRIGHTNESS
   179                                  ;USES	A,X,Y,(VGLIST,VGLIST+1),(XCOMP,XCOMP+3)
   180
   181   006A    84    73               VGVTR:  STY VGBRIT
   182   006C    A0    00               VGVTR1: LDY I,0
   183   006E    0A                             ASL
   184   006F    90    01                       BCC 10$                 ;SIGN EXTEND
   185   0071    88                             DEY                     ;Y=-1
   186   0072    84    6F               10$:    STY XCOMP+1             ;WITH SCALE=0 A=1 MEANS=DOTS ON XY
   187   0074    0A                             ASL
   188   0075    26    6F                       ROL XCOMP+1
   189   0077    85    6E                       STA XCOMP
   190   0079    8A                             TXA                     ;WRITE Y VALUE TO XCOMP+2,XCOMP+3
   191   007A    0A                             ASL
   192   007B    A0    00                       LDY I,0
   193   007D    90    01                       BCC 20$                 ;SIGN EXTEND
   194   007F    88                             DEY                     ;Y=-1
   195   0080    84    71               20$:    STY XCOMP+3
   196   0082    0A                             ASL
   197   0083    26    71                       ROL XCOMP+3
   198   0085    85    70                       STA XCOMP+2
   199   0087    A2    6E               VGVTR2: LDX I,XCOMP
   200                                  ;	JMP VGVCTR		;GENERATE VECTOR
   201
   202
   203                                          .SBTTL VGVCTR - ADD VECTOR TO VECTOR LIST
   204                                  ;VGVCTR - ADD VECTOR TO VECTOR LIST
   205                                  ;
   206                                  ;NOTE:	IF THE NUMBERS GIVEN ARE MORE THAN
   207                                  ;	15 BITS IN SIGNIFICANCE THEN AN
   208                                  ;	INCORRECT VECTOR WILL BE GENERATED
   209                                  ;
   210                                  ;ENTRY	(X) =4 ZERO PAGE LOCN.  CONTAINING (X LSB,X MSB,Y MSB,Y LSB)
   211                                  ;	(VGLIST,VGLIST+1) = VECTOR LIST ADDRESS
   212                                  ;	(VGBRIT)=VECTOR BRIGHTNESS (ONLY THE UPPER 3 BITS ARE USED)
   213                                  ;EXIT	(VGLIST,VGLIST+1) = NEW VECTOR LIST ADDRESS
   214                                  ;USES	A,X,Y,(VGLIST,VGLIST+1)
   215
   216   0089    A0    00               VGVCTR: LDY I,0
   217   008B    B5    02                       LDA ZX,2                ;Y LSB
   218   008D    91    74                       STA NY,VGLIST
   219   008F    B5    03                       LDA ZX,3                ;Y MSB
   220   0091    29    1F                       AND I,1F                ;CLEAR SIGN EXTENSION
   221   0093    C8                             INY
   222   0094    91    74                       STA NY,VGLIST
   223   0096    B5    00                       LDA ZX,0                ;X LSB
   224   0098    C8                             INY
   225   0099    91    74                       STA NY,VGLIST
   226   009B    B5    01                       LDA ZX,1                ;X MSB
   227   009D    45    73                       EOR VGBRIT
   228   009F    29    1F                       AND I,1F                ;CLEAR SIGN EXTENSION
ALVGUT	ATARI MAC65 VM03.09  00:01:07 PAGE 17+
VGVCTR - ADD VECTOR TO VECTOR LIST

   229   00A1    45    73                       EOR VGBRIT              ;COMBINE UPPER 3 BITS OF VGBRIT WITH X MSB
   230   00A3    C8                     VGWAI1: INY
   231   00A4    91    74                       STA NY,VGLIST           ;SET INTENSITY
   232   00A6    D0    AE                       BNE VGADD               ;ALWAYS - UPDATE VGLIST POINTER
   233
   234
   235                                          .SBTTL  DISPLAY DIGITS
   236                                  ;
   237                                  ; DIGITS - DISPLAY 2Y DIGIT NUMBER
   238                                  ;
   239                                  ; ENTRY	 (C)=CARRY SET FOR ZERO SUPPRESSION
   240                                  ;	 (A)=ADDRESS OF (Y) ZERO PAGE LOCATIONS CONTAINING NUMBER (LSB TO MSB)
   241                                  ;	 (Y)=NUMBER OF ZERO PAGE LOCATIONS TO USE (1 TO 256).
   242   00A8    38                     DIGTYS: SEC
   243   00A9    08                     DIGITS: PHP                     ;SAVE INPUT PARAMETERS
   244   00AA    88                             DEY
   245   00AB    84    AE                       STY ZPNLOC
   246   00AD    18                             CLC
   247   00AE    65    AE                       ADC ZPNLOC
   248   00B0    28                             PLP
   249   00B1    AA                             TAX                     ;MSB OF DIGITS
   250                                          BEGIN
   251   00B2    08                             PHP
   252   00B3    86    AF                       STX ZPOFFS              ;SAVE DIGIT ZP OFFSET
   253   00B5    B5    00                       LDA ZX,0
   254   00B7    4A                             LSR
   255   00B8    4A                             LSR
   256   00B9    4A                             LSR
   257   00BA    4A                             LSR
   258   00BB    28                             PLP
   259   00BC    20  0010'                      JSR VGHEXZ              ;FIRST DIGIT
   260   00BF    A5    AE                       LDA ZPNLOC
   261   00C1    D0    00                       IFEQ
   262   00C3    18                             CLC                     ;DISPLAY LAST DIGIT (EVEN 0)
   263   00C2    01                             ENDIF
   264   00C4    A6    AF                       LDX ZPOFFS
   265   00C6    B5    00                       LDA ZX,0
   266   00C8    20  0010'                      JSR VGHEXZ              ;SECOND DIGIT
   267   00CB    A6    AF                       LDX ZPOFFS
   268   00CD    CA                             DEX
   269   00CE    C6    AE                       DEC ZPNLOC
   270   00D0    10    E0                       MIEND                   ;LOOP FOR EACH SET OF DIGITS
   271   00D2    60                             RTS
   272                                          HLL65
   273         0001'                            .END
ALVGUT	ATARI MAC65 VM03.09  00:01:07 PAGE 17+
SYMBOL TABLE

ACTIP   03AA   		ALLPOT= 60C8   		ALLPO2= 60D8   		AUDCTL= 60C8   		AUDC1 = 60C1
AUDC2 = 60D1   		AUDF1 = 60C0   		AUDF2 = 60D0   		AUD2CT= 60D8   		BFACTR  0078
BINSCA  005A   		BLIFIN  0156   		BLUE  = 0006   		BLULET= 0007   		BOFLAS  0124
BONUS   0102   		BOOKKE  0412   		BOOKKS  0406   		BOOMFL  010D   		BOOMTI  010E
BUFACT  0415   		BUFRDY  0054   		CAMPC   010B   		CAMSTA  010C   		CBOOM = 0024
CBUF1 = 0078   		CDBOOM= 0004   		CDBOXP= 0012   		CDGETI= 0006   		CDGOVR= 000C
CDHITB= 000A   		CDLADR= 0014   		CDLOGP= 0014   		CDPLAY= 0000   		CDPLPL= 000E
CDPRST= 0010   		CDREQR= 0008   		CDROP = 0020   		CDSYST= 0002   		CD2GAM= 0016
CENDGA= 0008   		CENDLI= 0006   		CENDWA= 000E   		CGETIN= 0012   		CHACHA  00A7
CHACOU  0135   		CHAINV  00A8   		CHARCO  02F2   		CHARL1  02AD   		CHARL2  02C0
CHARY   02D3   		CHARYL  02E6   		CHISCH= 0010   		CINIRA= 001C   		CLOGO = 001A
CNEWAV= 000C   		CNEWGA= 0000   		CNEWLI= 0002   		CNEWV2= 0018   		CNOTFO= 0014
CNWLF2= 001E   		COCKTA= 0010   		COCTAL  0117   		COLOR   009E   		COLPOR= 0800
COLRAM  0019   		CPAUSE= 000A   		CPLAY = 0004   		CREQRA= 0016   		CSYSTM= 0022
CURCOL= 0001   		CURMOD  0106   		CURNTX  006A   		CURNTY  006C   		CURSL1  0200
CURSL2  0201   		CURSPO  0051   		CURSVH  0105   		CURSVL  0104   		CURSY   0202
CURSYL  0107   		CURWAV  009F   		DBSW    004C   		DEPCOL= 0006   		DIGITS  00A9 RG
DIGTYS  00A8 RG		EACTL = 6040   		EADAL = 6000   		EAIN  = 6050   		ELICNT  0123
EMCTRS= 0003   		ENSIZE  0151   		ESHCOU  00A6   		EXICAM  010A   		EXL     005E
EXPCOL= 0000   		EXPCOU  0116   		EXPLOL  02FA   		EXPLOS  0312   		EXPLOT  0302
EXPLOY  030A   		EYEFAC  0065   		EYH     005B   		EYL     005F   		EYLDES  005D
EYLL    005C   		EZL     0060   		FARY    014E   		FBLUE = 000B   		FGREEN= 0007
FLASH = 000F   		FLGNHI  0603   		FLICOL= 0003   		FLIPCO  0142   		FRED  = 000C
FRTIMR  0053   		FUSECO  0146   		GAMOP1  071E   		GAMOP3  071F   		GREEN = 0005
HARDWA= 6000   		HIRATE  0127   		HIWAVE  0126   		HRANKH= 0620   		HRANKL  061E
HRANKM= 061F   		HRED  = 000D   		HSCORH= 0708   		HSCORL= 0706   		HSCORM= 0707
ICHCOL= 0000   		ILINDD= 00F0   		ILINLI= 0010   		INCCOU  0109   		INDEX1  0037
INDEX2  0038   		INDEX3  0039   		INDEX4  003A   		INDYHI  003C   		INDYLO  003B
ININDX  0604   		INITAL  0606   		INMCOU  0108   		INOP0 = 0D00   		INOP1 = 0E00
INPUT   004A   		INTACK= 5000   		INTENS  0098   		INTIME  0128   		INVABI= 0007
INVACT  02A6   		INVAC1  0283   		INVAC2  028A   		INVAL1  02B9   		INVAL2  02CC
INVAY   02DF   		INVAYL  029F   		INVCAM  0291   		INVCAR= 0003   		INVCOL= 0003
INVDIR= 0080   		INVFIR= 0040   		INVLOO  0298   		INVMOT= 0080   		INVROT= 0040
INVSEQ= 0018   		IN1   = 0C00   		LEDOFF= 0003   		LEFSID  007B   		LETCOL= 0005
LEVELY  010F   		LIFSXH  035A   		LIFSXL  036A   		LIFSZH  037A   		LIFSZL  038A
LINANG  03EE   		LINEX   03CE   		LINEXM  0435   		LINEY   03AC   		LINEZ   03DE
LINEZM  0445   		LINSCA  0059   		LINSTA  039A   		LINSXH  031A   		LINSXL  032A
LINSZH  033A   		LINSZL  034A   		LITRAL  00AC   		LIVES1  0048   		LIVES2  0049
LSCORH  0042   		LSCORL  0040   		LSCORM  0041   		LVSGAM  0158   		MAH   = 6081
MAL   = 6080   		MATRAC= 0080   		MBH   = 6083   		MBL   = 6082   		MBSTAR= 6080
MCOINC= 0002   		MCOINL= 0004   		MCOINR= 0001   		MDITES= 0020   		MDYPL = 6098
MECHS = 0003   		MEH   = 6085   		MEL   = 6084   		MFAKE = 0080   		MFH   = 6087
MFIRE = 0010   		MFL   = 6086   		MFLIP = 0004   		MGTMOD= 0040   		MHALT = 0040
MLCCNT= 0004   		MLED1 = 0002   		MLED2 = 0001   		MMCCNT= 0002   		MNL   = 608C
MOPTI4= 0020   		MOPT13= 0007   		MRCCNT= 0001   		MSTAT = 6040   		MSTRT1= 0020
MSTRT2= 0040   		MSUZA = 0008   		MSZXD = 6094   		MTEMP   0031   		MTEST = 0010
MVINVX= 0008   		MVINVY= 0010   		MXH   = 6089   		MXL   = 6088   		MXPH  = 6096
MXPL  = 6095   		MYHIGH= 6070   		MYLOW = 6060   		MZHH  = 6090   		MZHL  = 608F
MZLH  = 608E   		MZLL  = 608D   		M3KHTI= 0080   		NCHARG= 000C   		NEARY   014D
NEOFLI  014F   		NEWAIT  00A3   		NEWPLA  003F   		NEXPLO= 0008   		NGAMES  0100
NGAME0  040E   		NGAMIH  040D   		NGAMIL  040C   		NGAM2H  0410   		NGAM2L  040F
NGAM20  0411   		NGAVGH  0413   		NGAVGL  0412   		NGAVGZ  0414   		NHISCO= 0008
NICHAR= 0004   		NINVAD= 0007   		NLINES= 0010   		NNYMPH= 0040   		NPARTI= 0010
NPCHAR= 0008   		NPLANE= 0008   		NPLAYR  0101   		NRANKS= 0063   		NROMS = 000C
NUMPLA  003E   		NWNYMC  015B   		NWTELI  015A   		NYMCOL= 000C   		NYMCOU  03AB
NYMPL   0203   		NYMPY   0243   		OBJIND  0055   		OCURSL  00A5   		OFRTIM  00A4
OLDLHI  00AB   		OLDLLO  00AA   		OLOFLI  0150   		OM2GAM= 0001   		OPFLIP  013D
ALVGUT	ATARI MAC65 VM03.09  00:01:07 PAGE 17+
SYMBOL TABLE

OPFUSE  0141   		OPPULS  013E   		OPSPIN  0140   		OPTANK  013F   		OPTIN1  0009
OPTIN2  000A   		OPTIN3  016A   		OTB     0052   		OUTANK= 60E0   		OUT0  = 4000
PARLIX  0203   		PARLIY  0223   		PARLIZ  0243   		PARLXA= 0020   		PARLXV  02C3
PARLYA= 0020   		PARLYV  02E3   		PARLZA= 0020   		PARLZV  0303   		PARTIX  0263
PARTIY  0283   		PARTIZ  02A3   		PARTXA= 0000   		PARTXV  0323   		PARTYA= 0000
PARTYV  0343   		PARTZA= 0000   		PARTZV  0363   		PCHCOL= 0001   		PCVELO= 0009
PDIRED= 000B   		PDIWHI= 0009   		PDIYEL= 000A   		PLAGRO  0115   		PLANEY  03FE
PLAYUP  003D   		POKEY = 60C0   		POKEY2= 60D0   		POTGO = 60CB   		POTGO2= 60DB
PROG  = 9000   		PSCALE  016B   		PSHCTR= 0008   		PUCHDE  00B2   		PULPOT  0157
PULSCO  0143   		PULSON  0148   		PULTIM  0147   		PULVEL= FEA0   		PURPLE= 0002
PXL     0056   		PYL     0057   		PZL     0058   		QDSTAT  0001   		QFRAME  0003
QNXTST  0002   		QSTATE  0000   		QSTATU  0005   		QTMPAU  0004   		QT1     00B5
QT2     016C   		QT3     0455   		QT4     0720   		QT5     011F   		QT6     011B
QUASEC= 0005   		RANDOM= 60CA   		RANDO2= 60DA   		RANKS   0600   		RED   = 0003
RITSID  007C   		ROMSTA= 3000   		ROTDIS  0114   		ROTFLG  0113   		RSCORH  0045
RSCORL  0043   		RSCORM  0044   		RUNGVG  00B0   		SAVEND  03CE   		SAVEP   03BC
SAVEX   0035   		SAVEY   0036   		SCFL    0079   		SECOND= 0014   		SECOPH  040B
SECOPL  0409   		SECOPM  040A   		SECOUH  0408   		SECOUL  0406   		SECOUM  0407
SECUVG  00B6   		SECUVY  016E   		SKCTL = 60CF   		SKCTL2= 60DF   		SPACG = 0000
SPARE3  0133   		SPBINA  013A   		SPFTIM  013C   		SPINCO  0145   		SPLINE  0139
SPOKST  0425   		SPXIND  013B   		SUBCOU  0099   		SUZCNT  03AA   		SUZTIM  0125
SVGLIS  0076   		SWFINA  004E   		SWRELE  004F   		SWSTAT  004D   		SWSTRT  004D
SXH     0062   		SXL     0061   		SYM   = 6092   		SZH     0064   		SZL     0063
S1    = 00B2 R 		S2    = 00C1 R 		TANCOL= 0002   		TANKCO  0144   		TBHD    0050
TBLIND  0602   		TCMFLG  00A2   		TEMPL   002A   		TEMPX   002E   		TEMPY   002F
TEMPZ   0030   		TEMP0   0029   		TEMP1   002A   		TEMP2   002B   		TEMP3   002C
TEMP4   002D   		TIMHIS  0605   		TNKOUT  00A1   		TOUT0   00B4   		TRACOL= 0005
TSLAMR  000B   		TURQOI= 0004   		TYPCOD  015E   		UNITXH  009B   		UNITXL  009A
UNITZH  009D   		UNITZL  009C   		VECRAM= 2000   		VGADD   0056 RG		VGADD2  004E RG
VGADD3  0050 RG		VGBRIT  0073  G		VGCNTR  004A RG		VGHALT  0004 RG		VGHAL1  0009 R
VGHEX   0016 RG		VGHEXZ  0010 RG		VGHEX1  001B R 		VGJSRL  0030 RG		VGLIST  0074  G
VGMSGA= ****  G		VGRTSL  0000 RG		VGSCAL  0063 RG		VGSCA1  0061 RG		VGSIZE  0072
VGSTAR= 4800   		VGSTAT  0043 RG		VGSTA1  0041 RG		VGSTOP= 5800   		VGVCTR  0089 RG
VGVTR   006A RG		VGVTR1  006C RG		VGVTR2  0087 RG		VGWAI1  00A3 R 		VGY     00A9
WAVEN1  0046   		WAVEN2  0047   		WCHAMX  011A   		WCHARF  0119   		WCHARI  0118
WCHARL  0120   		WELCOL= 0006   		WELLID  0112   		WELTYP  0111   		WFLICA  015D
WFLMAX  012E   		WFLMIN  0129   		WFUFRQ  015F   		WFUMAX  0132   		WFUMIN  012D
WFUSCH  0159   		WFUSIH= 0169   		WFUSIL= 0164   		WHITE = 0000   		WINVIL  0160
WINVIN  0165   		WINVJM  011D   		WINVMX  011C   		WNYMMX  011E   		WPULCA  015C
WPULFI  016D   		WPUMAX  012F   		WPUMIN  012A   		WSPMAX  0131   		WSPMIN  012C
WTACAR  0149   		WTAMAX  0130   		WTAMIN  012B   		WTCHDG= 5000   		WTFMAX  0136
WTTFRA  00B3   		XADJL   0066   		XCOMP   006E  G		X0H     0080   		X0L     0078
X1H     0081   		X1L     0079   		X2H     0082   		X2L     007A   		X3H     0083
X3L     007B   		X4H     0084   		X4L     007C   		X5H     0085   		X5L     007D
X6H     0086   		X6L     007E   		X7H     0087   		X7L     007F   		YCOMP = 0070
YDEUNI  00A0   		YELLOW= 0001   		ZABFLI= 0000   		ZABFUS= 0004   		ZABPUL= 0001
ZABTAN= 0002   		ZABTRA= 0003   		ZADEST  0121   		ZADJL   0068   		ZAPCOL= 0000
ZBLACK= 000F   		ZBLUE = 000B   		ZCARFL= 0001   		ZCARFU= 0003   		ZCARNO= 0000
ZCARPU= 0002   		ZDIRDO= 0080   		ZDIRUP= 0000   		ZFIRNO= 0000   		ZFIRYE= 0040
ZGREEN= 0007   		ZMOTJM= 0080   		ZMOTMO= 0000   		ZPNLOC  00AE   		ZPOFFS  00AF
ZPURPL= 0008   		ZRED  = 000C   		ZROCCW= 0040   		ZROTCW= 0000   		ZTURQO= 0003
ZWHITE= 0000   		ZYELLO= 0004   		Z0H     0090   		Z0L     0088   		Z1H     0091
Z1L     0089   		Z2H     0092   		Z2L     008A   		Z3H     0093   		Z3L     008B
Z4H     0094   		Z4L     008C   		Z5H     0095   		Z5L     008D   		Z6H     0096
Z6L     008E   		Z7H     0097   		Z7L     008F   		$BC     0018   		$BCCNT  0016
$CCTIM  0013   		$CMODE  0009   		$CNCT   0017   		$CNSTT  000D   		$COINA  0008
$INTCT  0007   		$LAM    0008   		$LMBIT= 0008   		$LMTIM  000C   		$PSTSL  0010
ALVGUT	ATARI MAC65 VM03.09  00:01:07 PAGE 17+
SYMBOL TABLE

$TEST   0008   		$$CRDT  0006   		...RD = 0010   		...T  = 00C4 R 		...X  = 0000

. ABS.	0721	  00
      	00D3	  01
ERRORS DETECTED: 0
FREE CORE: 12922. WORDS

OBJ:ALVGUT,OBJ:ALVGUT.LST=ALVGUT

(OBJ:ALVGUT.OBJ,OBJ:ALVGUT.LST=DK1:ALVGUT)

A TOTAL OF 1,729 STATEMENTS WERE PROCESSED.

CPU TIME -  00:00:00.0     I/O TIME -  00:00:00.0

                                                                      