.SBTTL	VGMC - VECTOR MACROS
	.SBTTL	**************************
	.SBTTL	*
	.SBTTL	* PROGRAMMER:  ED LOGG
	.SBTTL	*
	.SBTTL	**************************
	.REPT	0
DATE:		8-OCT-79
PROJECT:	23803
DISK:		105, B39
DESCRIPTION:
	GENERAL PURPOSE MACROS TO FACILITATE PROGRAMMING THE
	AUTO-NORMALIZING VECTOR GENERATOR

	BEWARE:  CODE REFERENCING THESE MACROS MUST BE ASECT IF THE "JSRL"
		 OR "JMPL" MACRO IS USED

	.ENDR
;
;	CNTR - CENTER BEAM IN MIDDLE OF SCREEN.
;
	.SBTTL CNTR
	.MACRO CNTR 
	.WORD ^H8040
	.ENDM
;
;	HALT - HALT THE VECTOR GENERATOR INSTRUCTION
;	WHEN EXECUTED BY VG SETS THE VG HALT FLAG.
;
	.SBTTL	HALT
	.MACRO	HALT
	.WORD	^H2000
	.ENDM
;
;	JSRL - JUMP SUBROUTINE INSTRUCTION
;	THE LOWER THREE NYBBLES SPECIFY AN ADDRESS RELATIVE
;	TO THE BEGINNING OF THE VECTOR GENERATOR ADDRESSABLE
;	INSTRUCTION SPACE  DIVIDED BY 2.  THE TOTAL ADDRESS
;	AREA IS 8K BYTES.
;
	.SBTTL	JSRL
	.MACRO	JSRL LABL
	.WORD	<LABL&^H1FFF>/2.+^H0A000
	.ENDM
;
;	STAT - SET VECTOR GENERATOR STATUS
;	THE FIRST PARAMETER IS AN INTENSITY TO SET THE INTENSITY REGISTER.
;	THE SECOND PARAMETER IS CALLED HI/LOW.  THE CURRENT BEAM POSITION
;	IS USED TO DEFINE THE CORNERS OF A RECTANGULAR AREA.  IF HI/LOW=1,
;	THEN THIS DEFINES THE UPPER RIGHT CORNER; OTHERWISE, IT IS THE
;	LOWER LEFT CORNER.  THE THIRD PARAMETER IS CALLED IN/OUT.  IF IT
;	IS 0, THEN LINES ARE DISPLAYED ON THE INSIDE ELSE THE OUTSIDE.
;
	.SBTTL STAT
	.MACRO STAT Z,HI,IN
	...1=1
	...2=0
	...3=0
	.IF NB,HI'IN
	...1=0
	...2=HI
	...3=IN
	.ENDC
	.WORD ^H6000+<...1*^H400>+<...2*^H200>+<...3*^H100>+<Z*^H10>
	.ENDM
;
;	SCAL - SET VECTOR GENERATOR SCALE
;	THE FIRST PARAMETER IS THE POWER OF 2 SCALE.  A VALUE OF 0 MEANS
;	FULL SIZE, 1=1/2 SIZE, 2=1/4, ..., 7=1/128 SIZE.  THE SECOND
;	PARAMETER IS THE LINEAR SCALE (OPTIONAL).  A VALUE OF 0 MEANS
;	FULL SIZE, 80=1/2 SIZE, FF=1/256 SIZE.
;
	.SBTTL SCAL
	.MACRO SCAL S,LS
	...1=0
	.IIF NB,LS,...1=LS
	.WORD ^H7000+...1+<S*^H100>
	.ENDM
;
;	RTSL - RETURN FROM SUBROUTINE 
;	THIS INSTRUCTION IS ANALOGOUS TO AN RTS INSTRUCTION FOR
;	A MICROPROCESSOR. THE VG IS CAPABLE OF FIVE LEVELS OF
;	SUBROUTINE CALLS (JSRL).
;
	.SBTTL	RTSL
	.MACRO	RTSL
	.WORD	^H0C000
	.ENDM
;
;	JMPL - JUMP TO ROUTINE
;	THE LOWER THREE BYTES REFERENCES AN ADDRESS JUST AS
;	THE LOWER THREE BYTES OF AN JSRL INSTRUCTION. THE
;	INSTRUCTION OPERATES JUST AS A JMP INSTRUCTION DOES
;	FOR A 650X OR 6800 MICRO.
;
	.SBTTL	JMPL
	.MACRO	JMPL LABL
	.WORD	<LABL&^H1FFF>/2.+^H0E000
	.ENDM
;
;	VCTR - DRAW VECTOR INSTRUCTION
;	THIS INSTRUCTION DRAWS A VECTOR ON THE DISPLAY AREA
;	RELATIVE TO THE PREVIOUS BEAM POSITION BEFORE THE
;	INSTRUCTION IS EXECUTED.  DX IS THE CHANGE IN BEAM X
;	POSITION; DY IS THE CHANGE IN BEAM Y POSITION; ZZ 
;	SPECIFIES THE BEAM INTENSITY (0 THROUGH 7., 0 IS
;	NO INTENSITY, 7. IS BRIGHTEST INTENSITY).
;
	.SBTTL	VCTR
	.MACRO	VCTR DX,DY,ZZ
	...1=DX
	...2=DY
	.IF	 LT,...1		;IF NEGATIVE X
	...1=-DX
	.ENDC
	.IF	 LT,...2		;IF NEGATIVE Y
	...2=-DY
	.ENDC
	...5=...1!...2
	.IF	NE,...1+...2			;IF X AND Y ARE 0 - SKIP SHOR VECTOR
	.IF	 EQ,...5&^H0FFE1
	.WORD	^H4000+<ZZ*^H20>+<DX/2&^H1F>+<DY*^H80&^H1F00>
	.MEXIT
	.ENDC
	.ENDC
	.WORD	DY&^H1FFF,<ZZ*^H2000>+<DX&^H1FFF>
	.ENDM
;
;	ALPHA - ALPHANUMERIC JSRL LIST INSTRUCTION
;	THIS INSTRUCTION CONSTRUCTS A JSRL INSTRUCTION
;	FOR EVERY CHARACTER IN THE CALLING PARAMETER
;	STRING, THUS A LABEL CHAR.X MUST EXIST FOR EVERY
;	UNIQUE CHARACTER X IN THE PARAMETER STRING.
;	RESERVED CHARACTERS USED BY THE PDP SYSTEMS 
;	(IE @ : ; ETC.) SHOULD NOT BE USED.
;
	.SBTTL	ALPHANUMERICS
	.MACRO	ALPHA	STRING
	.IRPC	...X,<STRING>
	JSRL	CHAR.'...X
	.ENDR
	.ENDM
	.LIST MEB
	.NLIST	BYT
	.MACRO	COLOR	COL,LUM
	.BYTE	<LUM*^H10>+COL,^H64
	.ENDM
;
;	COLOR - THE COLOR INSTRUCTION REPLACES THE STAT
;	INSTRUCTION IN THE COLOR VECTOR HARDWARE.  TWO PARAMETERS
;	ARE PASSED.  THE FIRST IS THE DESIRED COLOR, THE
;	SECOND IS THE INTENSITY.
;	TYPICAL FORMAT:	COLOR	RED,10
;
	.PAGE
