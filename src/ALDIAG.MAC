.TITLE	ALDIAG - TEMPEST VG DIAGNOSTIC
	.SBTTL	*************************************************
	.SBTTL	*						*
	.SBTTL	*	MODULE:ALDIAG				*
	.SBTTL	*	FUNCTION:STAND ALONE VG DIAGNOSTIC PROM	*
	.SBTTL	*						*
	.SBTTL	*************************************************
	.SBTTL	*						*
	.SBTTL	*	PROGRAM MODIFIED BY			*
	.SBTTL	*	DOUG SNYDER	9/11/81			*
	.SBTTL	*						*
	.SBTTL	*************************************************
	.NLIST
	.INCLUDE ALCOMN			;<<<<<<<<<<<<<<<<<<<<<<<<<
	.LIST
	.INCLUDE VGMC			;<<<<<<<<<<<<<<<<<<<<<<<<<
;LINKED ALONE
	.ASECT
	.=0D800
;
;USES NO WORKING RAM (0 TO 7FF)
;
TEST:
	SEI			;DISABLE INTERRUPTS
	CLD			;CLEAR DECIMAL MODE
	LDX I,0FF
	TXS			;SET STACK
	LDA I,MVINVY		;INITIALIZE X,Y AXIS FLIP BITS
	STA OUT0
	LDX I,3			;JSRL, RTSL TEST KLUDGE
	BEGIN
	LDA X,TBL4A
	STA X,PNTR4A
	LDA X,TBL4B
	STA X,PNTR4B
	LDA X,TBL4C
	STA X,PNTR4C
	DEX
	MIEND
	LDA I,0
	BEGIN			;LOOP AT 1 KHZ RATE
	STA WTCHDG
	LDX I,01
	BEGIN			;LOOP FOR 1 MS
	LDY I,153.
	BEGIN
	DEY
	EQEND
	DEX
	EQEND
	STA VGSTOP
	CMP INOP0		;GET OPTION SWITCHES
	IFNE			;SAME AS BEFORE?
	LDA INOP0		;NO.
	LDX I,8
	SEC
	BEGIN			;LOOP UNTIL SET BIT IS FOUND OR 8X
	ROR
	IFCS			;SET BIT?
	LDY X,POINTR		;YES. GET OFFSET OF LAST BYTE
	LDA X,COUNT		;GET # OF VG BYTES
	TAX
	BEGIN			;TRANSFER TO VECTOR RAM
	LDA Y,VECINS
	STA X,VECRAM
	DEY
	DEX
	MIEND
	ENDIF
	DEX
	MIEND
	LDA INOP0
	ENDIF
	STA VGSTART
	CLC
	CSEND			;ALWAYS LOOP
	.PAGE
	.SBTTL	VG INSTRUCTION SETS
VECINS:
T1S:	HALT			;TEST HALT
T1E:
;
T2S:	SCAL 1,0		;TEST
	CNTR
	VCTR 100,100,3
	CNTR
	HALT
T2E:
;
T3S:	JMPL NEXT-T3S		;TEST JMP AND JSR
NEXT:	JSRL SKIPH-T3S
	HALT
SKIPH:	SCAL 1,0
	CNTR
	VCTR -100,100,4
	CNTR
	RTSL
T3E:
;
T4S:				;TEST VJSRL NESTING
	JSRL PNTR4B-VECRAM		;*** SEE SNYDER ***
	SCAL 1,0
	CNTR
	VCTR -100,-100,4
	CNTR
	HALT
	RTSL
T4E:
;
T5S:				;TEST BINARY SCALING
	SCAL 1,0
	CNTR
	SCAL 0,0
	JSRL VECTOR-T5S
	SCAL 1,0
	JSRL VECTOR-T5S
	SCAL 2,0
	JSRL VECTOR-T5S
	SCAL 3,0
	JSRL VECTOR-T5S
	SCAL 4,0
	JSRL VECTOR-T5S
	HALT
VECTOR:	VCTR 0,40,3
	VCTR 20,-40,0
	RTSL
T5E:
T6S:	HALT
T6E:
T7S:	HALT
T7E:
T8S:	HALT
T8E:
T9S:	HALT
T9E:
COUNT:	.BYTE T1E-T1S-1		;# OF BYTES IN SET-1
	.BYTE T2E-T2S-1
	.BYTE T3E-T3S-1
	.BYTE T4E-T4S-1
	.BYTE T5E-T5S-1
	.BYTE T6E-T6S-1
	.BYTE T7E-T7S-1
	.BYTE T8E-T8S-1
	.BYTE T9E-T9S-1
POINTR:	.BYTE T1E-VECINS-1			;LOC OF LAST BYTE OF VG INSTRUCTION SET
	.BYTE T2E-VECINS-1
	.BYTE T3E-VECINS-1
	.BYTE T4E-VECINS-1
	.BYTE T5E-VECINS-1
	.BYTE T6E-VECINS-1
	.BYTE T7E-VECINS-1
	.BYTE T8E-VECINS-1
	.BYTE T9E-VECINS-1
PNTR4A	=24B4			;JSRL, RTSL TEST KLUDGE
TBL4A:	JSRL PNTR4C-VECRAM
	RTSL

PNTR4B	=2B4A
TBL4B:	JSRL PNTR4A-VECRAM
	RTSL

PNTR4C	=2FF0
TBL4C:	JSRL T4E-T4S-2
	RTSL
	.VCTRS 0DFFA,TEST,TEST,TEST
	HLL65
	.END
